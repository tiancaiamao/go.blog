<p>宏在lisp里面是一柄双刃剑，我一般是能避免使用尽量避免的，也在一直思考有没有什么能替代它。上次看到了fexpr，研究了一下，知道了fexpr和宏的区别。它们都不会对参数求值，都是把参数以原始的list形式传递。接着，它们都会调用自己的定义，对list进行展开，这一步也是相同的。最后一步才是区别：宏会对展开以后生成的表达式求值，而fexpr直接返回展开的结果。</p><p>这个设定使得fexpr比宏更加动态，使语言无法静态分析，编译期更加模糊，或者说，编译器跟运行期搅和更严重，无法脱离eval。所以似乎也不是一个我想要的东西。</p><p>如果没有宏，函数的极限在哪里？</p><p>以实现pattern match为例，我想这样写：</p><pre><code>(pmatch x
        ((:cons a b) (+ a b))
        ((:nil) 42))
</code></pre><p>如果x是一个cons，pattern match会将a绑定到第一个，将b绑定到第二个，然后执行<code>(+ a b)</code>。没有宏的情况下，最接近能做到这个程度：</p><pre><code>(pmatch (cons 1 2)
        (:cons (lambda (a b) (+ a b)))
        (:nil (lambda () 42)))
</code></pre><p>如果判断是cons，则在一个函数里面计算<code>(+ a b)</code>，则<code>:cons</code>负责提供函数的参数，a绑定到第一个，b绑定到第二个。</p><p>先摆最终代码，别看代码不长，这可花了我一晚上才写出来。</p><pre><code>(define (selector is? . get)
  (lambda (f)
    (lambda (x)
      (lambda (cc)
        (if (is? x)
            (apply f (map (lambda (f) (f x)) get))
            (cc x))))))

(define pmatch
  (lambda (x . sel)
    (if (null? sel)
        (error 'panic)
        (((car sel) x)
        (lambda (x0)
          (apply pmatch (cons x0 (cdr sel))))))))

(define :cons (selector pair? car cdr))
(define :nil (selector null?))
</code></pre><hr /><p>TL;DR</p><p>lisp是高度抽象的，代码很短，背后的思维却异常的复杂。如果别人不告诉你怎么推出来的，代码跟本看不懂。</p><p>首先，pmatch函数的参数，应试是一个值，后面跟若干个函数，后面的函数依次对传值x进行计算，如果通过了就结束，没通过就继续尝试下一个。</p><pre><code>(pmatch (cons 1 2) sel1 sel2)
</code></pre><p>然后想sel1，sel2展开以后，应该是类似于这样子：</p><pre><code>(lambda (x)
  (if (pair? x)
      ((lambda (a b) (+ a b)) (car x) (cdr x))
      ?))
(lambda (x)
  (if (null? x)
      ((lambda () 42))
      ?))
</code></pre><p>由于要依次比较，我们要做一个CPS变换(这是个重点)，得到</p><pre><code>(lambda (cc)
  (lambda (x)
    (if (pair? x)
        ((lambda (a b) (+ a b)) (car x) (cdr x))
        (cc x))))
</code></pre><p>完整的写下来：</p><pre><code>(lambda (x)
  (let* ((:cons (lambda (cc)
                  (lambda (x)
                    (if (pair? x)
                        (      (lambda (a b) (+ a b))     (car x) (cdr x))
                        (cc x)))))
        (:nil (lambda (cc)
                (lambda (x)
                  (if (null? x)
                      (       (lambda () 42)    )
                      (cc x)))))
        (cc0 (lambda (_) (error 'panic)))
        (cc1 (:nil cc0))
        (cc2 (:cons cc1)))
    (cc2 x)))
</code></pre><p>验证一下，拿它个调用<code>(cons 1 2)</code>得到3，调用<code>'()</code>得到42，好，继续往下。</p><pre><code>(lambda (x)
  (let* ((:cons (lambda (cc)
                  (lambda (x)
                    (if (pair? x)
                        (      ????     (car x) (cdr x))
                        (cc x)))))
        (:nil (lambda (cc)
                (lambda (x)
                  (if (null? x)
                      (       ????    )
                      (cc x)))))
        (cc0 (lambda (_) (error 'panic)))
        (cc1 (:nil cc0))
        (cc2 (:cons cc1)))
    (cc2 x)))
</code></pre><p><code>????</code>是要作为参数传进去的，我们提取成参数<code>f</code>和<code>g</code>，再交换一下参数顺序，就变成了：</p><pre><code>(let* ((:cons (lambda (f)
                (lambda (x)
                  (lambda (cc)

                    (if (pair? x)
                        (f (car x) (cdr x))
                        (cc x)))
                  )))
      (f (lambda (a b) (+ a b)))
      (:nil (lambda (f)
              (lambda (x)
                (lambda (cc)

                  (if (null? x)
                      (f)
                      (cc x)))
                )))
      (g (lambda () 42))

      (cc0 (lambda (_) (error 'panic)))
      (cc1 (lambda (x) (((:nil g) x) cc0)))
      (cc2 (lambda (x) (((:cons f) x) cc1))))

  (cc2 x))
</code></pre><p>上面的<code>:cons</code>和<code>:nil</code>是可以单独拿出来的：</p><pre><code>(define :nil
  (lambda (f)
    (lambda (x)
      (lambda (cc)
        (if (null? x)
            (f)
            (cc x)))
      )))

(define :cons
  (lambda (f)
    (lambda (x)
      (lambda (cc)

        (if (pair? x)
            (f (car x) (cdr x))
            (cc x)))
      )))
</code></pre><p>接下来又是个重点，<code>:nil</code>和<code>:cons</code>结构是比较类似的，我希望只保留必须的代码，尽量不要重复。</p><p>必要的信息包括判断是否是某个类型<code>pair?</code>，<code>null?</code>，以及取结构里面某个域<code>car</code>，<code>cdr</code>。</p><pre><code>(define :cons (selector pair? car cdr))
(define :cons
  (lambda (f)
    (lambda (x)
      (lambda (cc)

        (if (pair? x)
            (f (car x) (cdr x))
            (cc x)))
      )))
</code></pre><p>需要让两者是等价的。那么如何定义<code>selector</code>呢？</p><pre><code>(define (selector pair? car cdr)
  (lambda (f)
    (lambda (x)
      (lambda (cc)
        (if (pair? x)
            (f (car x) (cdr x))
            (cc x))))))

(define (selector nil?)
  (lambda (f)
    (lambda (x)
      (lambda (cc)
        (if (nil? x)
            (f)
            (cc x))))))
</code></pre><p>于是抽象出<code>selector</code>的定义：</p><pre><code>(define (selector is? . get)
  (lambda (f)
    (lambda (x)
      (lambda (cc)
        (if (is? x)
            (apply f (map (lambda (f) (f x)) get))
            (cc x))))))
</code></pre><p>接下来看<code>pmatch</code>，它应该是这样子的：</p><pre><code>(define pmatch
  (lambda (x sel1 sel2)
    (let* ((cc0 (lambda (_) (error 'panic)))
          (cc1 (lambda (x) ((sel2 x) cc0)))
          (cc2 (lambda (x) ((sel1 x) cc1))))
      (cc2 x))))
</code></pre><p>其中<code>sel1</code>和<code>sel2</code>分别是：</p><pre><code>(:cons (lambda (a b) (+ a b)))
(:nil (lambda () 42))
</code></pre><p>写成let不太方便，如果把pmatch的let展开，就是:</p><pre><code>(define pmatch
  (lambda (x sel1 sel2)
    ((sel1 x)
    (lambda (x)
      ((sel2 x)
        (lambda (_)
          (error 'panic)))))))
</code></pre><p>最后为了处理许多的selector，把参数做变成长的：</p><pre><code>(define pmatch
  (lambda (x . sel)
    (if (null? sel)
        (error 'panic)
        (((car sel) x)
        (lambda (x0)
          (apply pmatch (cons x0 (cdr sel))))))))
</code></pre><p>推导结束，以上！</p><hr /><p>再放几个好玩的，比如说定义option类型</p><pre><code>(define :some (selector vector? (lambda (x) (vector-ref x 0))))
(define :none (selector null?))
(pmatch #(5)
        (:some (lambda (x) x))
        (:none (lambda () 42)))
</code></pre><p>再比如实现else：</p><pre><code>(define :else (selector (lambda (x) #t) (lambda (x) x)))
(pmatch '(a b c)
        (:cons (lambda (a b) (+ a b)))
        (:nil (lambda () 42))
        (:else (lambda (x) x)))
</code></pre>